# ftp configuration
:local ftphost "ip_ftp_server"
:local ftpport "2112"
:local ftpuser "user_ftp_server"
:local ftppassword "password_ftp_server"
:local ftppath "/destination/folder/example/"
#
# STOP EDITING!!
#
# Preparation: Auto generate months, date & time
:local months ("jan","feb","mar","apr","may","jun","jul","aug","sep","oct","nov","dec");
:local ts [/system clock get time]
:set ts ([:pick $ts 0 2].[:pick $ts 3 5].[:pick $ts 6 8])
:local ds [/system clock get date]
:local month [ :pick $ds 0 3 ];
:local mm ([ :find $months $month -1 ] + 1);
:if ($mm < 10) do={ :set mm ("0" . $mm); }
:set ds ([:pick $ds 7 11] . $mm . [:pick $ds 4 6])

# Auto generate file name for system backup - file name will be "Backup-servername-date-time"
:local autoname1 ("/Backup-".[/system identity get name]."-".$ds."-".$ts.".backup")
:local autoname2 ("/Backup-".[/system identity get name]."-".$ds."-".$ts.".rsc")
# Preparation: Auto generate log info
/system backup save name=$autoname1
:log info message="System backup finished (1/2).";
/export compact file=$autoname2
:log info message="Config export finished (2/2)."

# Start upload system backup file
:log info message="Uploading system backup (1/2)."
/tool fetch address="$ftphost" port=$ftpport src-path=$autoname1 user="$ftpuser" mode=ftp password="$ftppassword" dst-path="$ftppath/$autoname1" upload=yes
# Start upload exported config
:log info message="Uploading config export (2/2)."
/tool fetch address="$ftphost" port=$ftpport src-path=$autoname2 user="$ftpuser" mode=ftp password="$ftppassword" dst-path="$ftppath/$autoname2" upload=yes

# Delay time to finish the upload - (Big File or Slow Network Condition)
:delay 60s;
# Remove local backup file
:foreach i in=[/file find] do={ :if ([:typeof [:find [/file get $i name] "Backup-"]]!="nil") do={/file remove $i}; }
:log info message="Configuration backup finished.";
